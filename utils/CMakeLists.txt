## Etterfilter

set(EF_SRC
            etterfilter/ef_compiler.c
            etterfilter/ef_ec_compat.c
            etterfilter/ef_encode.c
            etterfilter/ef_main.c
            etterfilter/ef_output.c
            etterfilter/ef_parser.c
            etterfilter/ef_tables.c
            etterfilter/ef_test.c
)

find_package(BISON)
find_package(FLEX)
BISON_TARGET(EF_PARSER etterfilter/ef_grammar.y ${CMAKE_CURRENT_BINARY_DIR}/ef_grammar.c)
FLEX_TARGET(EF_SCANNER etterfilter/ef_syntax.l ${CMAKE_CURRENT_BINARY_DIR}/ef_syntax.c)
ADD_FLEX_BISON_DEPENDENCY(EF_SCANNER EF_PARSER)

# For ef_grammar.h
include_directories(${CMAKE_CURRENT_BINARY_DIR})

add_executable(etterfilter ${EF_SRC} ${FLEX_EF_SCANNER_OUTPUTS} ${BISON_EF_PARSER_OUTPUTS})
if(WIN32)
  target_link_libraries(etterfilter lib_ettercap ws2_32)
  target_link_libraries(etterfilter "-Wl,--subsystem,windows")
else()
  target_link_libraries(etterfilter lib_ettercap)
endif()
if(NOT DISABLE_RPATH)
  set_target_properties(etterfilter PROPERTIES INSTALL_RPATH ${INSTALL_LIBDIR})
endif(NOT DISABLE_RPATH)

## Etterlog

set(EL_SRC
            etterlog/el_analyze.c
            etterlog/el_conn.c
            etterlog/el_decode.c
            etterlog/el_decode_http.c
            etterlog/el_display.c
            etterlog/el_ec_compat.c
            etterlog/el_log.c
            etterlog/el_main.c
            etterlog/el_parser.c
            etterlog/el_profiles.c
            etterlog/el_stream.c
            etterlog/el_target.c
)

add_executable(etterlog ${EL_SRC})
target_link_libraries(etterlog lib_ettercap)
if(WIN32)
target_link_libraries(etterlog "-Wl,--subsystem,windows")
endif()
if(NOT DISABLE_RPATH)
  set_target_properties(etterlog PROPERTIES INSTALL_RPATH ${INSTALL_LIBDIR})
endif(NOT DISABLE_RPATH)
install(TARGETS etterfilter etterlog DESTINATION ${INSTALL_BINDIR})
